CMD_LEN = 3
SPRITE_0_VECTOR = 2040
SPRITE_0_BLOCK = $3F
SPRITE_0_ADDRESS = SPRITE_0_BLOCK * $40
VIC_BASE = $D000
JOYSTICK2 = $DC00
SPRITE_COLOR = 2

SUPER_CPU = 1

!ifndef SUPER_CPU {
NUM_DELAY_LOOPS = 15
} else {
NUM_DELAY_LOOPS = 250
}

.COMMAND
!byte 5
!tx "CMD> "

.CMD_CHARS
!byte 26
!tx "ABCDEFGHIJKLMNOPQRSTUVWXYZ"

.CMD_BUFFER
!byte CMD_LEN
!byte 0,0,0

.EXIT_COMMAND
!byte 1
!tx "X"

.VAL_COMMAND
!byte CMD_LEN
!tx "VAL"

.RUN_COMMAND
!byte CMD_LEN
!tx "RUN"

.LOAD_COMMAND
!byte CMD_LEN
!tx "LOD"

.SAVE_COMMAND
!byte CMD_LEN
!tx "SAV"

.SHOW_COMMAND
!byte CMD_LEN
!tx "SHO"

.PIXEL_COMMAND
!byte CMD_LEN
!tx "PXL"

.SELECT_PIXEL_COMMAND
!byte CMD_LEN
!tx "SXL"

.INC_ZOOM_COMMAND
!byte CMD_LEN
!tx "INZ"

.MULTICOLOR_COMMAND
!byte CMD_LEN
!tx "MCL"

.DOUBLE_X_STEP_COMMAND
!byte CMD_LEN
!tx "DBX"

.SET_ITER_COMMAND
!byte CMD_LEN
!tx "SIT"

.HIRES_COMMAND
!byte CMD_LEN
!tx "HIR"

.HALVE_X_STEP_COMMAND
!byte CMD_LEN
!tx "HVX"

.RESET_COMMAND
!byte CMD_LEN
!tx "RES"

.SET_DEVICE_COMMAND
!byte CMD_LEN
!tx "DEV"

.SET_COLS_COMMAND
!byte CMD_LEN
!tx "SCL"

.SHOW_COLS_COMMAND
!byte CMD_LEN
!tx "GCL"

.HELP_COMMAND
!byte 1
!tx "H"


.COMMAND_NOT_RECOGNIZED
!byte 22
!tx "COMMAND NOT RECOGNIZED"

.ALPHA
!byte 50
!tx "0123456789ABCDEFGHIJKLMNOPQRSTUVWXYZ.:-+#=() ยง$%&@"

.DIGITS
!byte 10
!tx "0123456789"


.CMD_TABLE 
!byte <.VAL_COMMAND, >.VAL_COMMAND, <showValuesCommand, >showValuesCommand
!byte <.RUN_COMMAND, >.RUN_COMMAND, <runCalcCommand, >runCalcCommand
!byte <.LOAD_COMMAND, >.LOAD_COMMAND, <loadCommand, >loadCommand
!byte <.SAVE_COMMAND, >.SAVE_COMMAND, <saveCommand, >saveCommand
!byte <.SHOW_COMMAND, >.SHOW_COMMAND, <showCommand, >showCommand
!byte <.PIXEL_COMMAND, >.PIXEL_COMMAND, <deriveFromPixelCommand, >deriveFromPixelCommand
!byte <.INC_ZOOM_COMMAND, >.INC_ZOOM_COMMAND, <incrementZoomCommand, >incrementZoomCommand
!byte <.MULTICOLOR_COMMAND, >.MULTICOLOR_COMMAND, <multicolorCommand, >multicolorCommand
!byte <.DOUBLE_X_STEP_COMMAND, >.DOUBLE_X_STEP_COMMAND, <doubleXstepCommand, >doubleXstepCommand
!byte <.SET_ITER_COMMAND, >.SET_ITER_COMMAND, <setMaxIterCommand, >setMaxIterCommand
!byte <.HIRES_COMMAND, >.HIRES_COMMAND, <hiresCommand, >hiresCommand
!byte <.HALVE_X_STEP_COMMAND, >.HALVE_X_STEP_COMMAND, <halveXstepCommand, >halveXstepCommand
!byte <.RESET_COMMAND, >.RESET_COMMAND, <resetCommand, >resetCommand
!byte <.SET_DEVICE_COMMAND, >.SET_DEVICE_COMMAND, <setDeviceNumberCommand, >setDeviceNumberCommand
!byte <.SET_COLS_COMMAND, >.SET_COLS_COMMAND, <setColorsCommand, >setColorsCommand
!byte <.SHOW_COLS_COMMAND, >.SHOW_COLS_COMMAND, <showColorsCommand, >showColorsCommand
!byte <.SELECT_PIXEL_COMMAND, >.SELECT_PIXEL_COMMAND, <selectPixelCommand, >selectPixelCommand
!byte <.HELP_COMMAND, >.HELP_COMMAND, <helpCommand, >helpCommand
!byte 0,0,0,0 ; end marker


; --------------------------------------------------
; readEvalPrintLoop
; --------------------------------------------------
readEvalPrintLoop
.cmdLoop
    +printStr .COMMAND
    lda #CMD_LEN
    sta .CMD_BUFFER
    +inputStr .CMD_BUFFER, .CMD_CHARS
    jsr printCRLF
    lda .CMD_BUFFER
    beq .cmdLoop                                       ; do noting when user just pressed enter
    +strCmpAddr .EXIT_COMMAND, .CMD_BUFFER
    beq .done
    jsr processCommand
    jmp .cmdLoop
.done
    rts

.CMD_COUNTER
!byte 0
.CMD_VECTOR
!byte 0,0
.NO_COMMAND
!byte 0,0
; --------------------------------------------------
; processCommand
; --------------------------------------------------
processCommand
    lda #<.CMD_BUFFER
    sta TEMP_PTR2
    lda #>.CMD_BUFFER
    sta TEMP_PTR2+1

    ldx #0
    stx .CMD_COUNTER
.testCommand
    ldx .CMD_COUNTER
    lda .CMD_TABLE,x
    sta TEMP_PTR
    inx
    lda .CMD_TABLE,x
    sta TEMP_PTR+1
    +cmp16Bit .NO_COMMAND, TEMP_PTR
    beq .notFound
    txa
    pha
    jsr strCmp
    bne .nextCommand
    pla
    tax
    inx
    lda .CMD_TABLE,X
    sta .CMD_VECTOR
    inx    
    lda .CMD_TABLE,X
    sta .CMD_VECTOR+1
    jmp (.CMD_VECTOR)
.nextCommand
    pla
    tax
    inx
    inx
    inx
    stx .CMD_COUNTER
    jmp .testCommand 

.notFound
    +printStr .COMMAND_NOT_RECOGNIZED
    jsr printCRLF
    rts

.REAL_TXT
!byte 8
!tx "REAL  : "
.IMAG_TXT
!byte 8
!tx "IMAG  : "
.STEPX_TXT
!byte 8
!tx "STEP X: "
.STEPY_TXT
!byte 8
!tx "STEP Y: "
.ITER_TXT
!byte 8
!tx "ITER  : "
.DEVICE_TXT
!byte 8
!tx "DEVICE: "


; --------------------------------------------------
; showValuesCommand
; --------------------------------------------------
showValuesCommand
    +printStr .REAL_TXT
    +printFixedPoint INIT_REAL
    +printStr .IMAG_TXT
    +printFixedPoint INIT_IMAG
    +printStr .STEPX_TXT
    +printFixedPoint STEP_X
    +printStr .STEPY_TXT
    +printFixedPoint STEP_Y
    +printStr .ITER_TXT
    lda MAX_ITER
    jsr printByteCRLF
    +printStr .DEVICE_TXT
    lda DEVICE_NUM
    jsr printByteCRLF

    rts


; --------------------------------------------------
; runCalcCommand
; --------------------------------------------------
runCalcCommand
    jsr initMandel
    jsr bitMapOn

.loopUntilFinished
    jsr nextMandel
    lda COUNT_Y
    cmp MAX_Y
    bne .loopUntilFinished

    jsr bitMapOff
    rts


.ERR_FILE_NAME_EMPTY
!byte 15
!tx "FILE NAME EMPTY"

.FILE_NAME
!byte 16
!tx "                "
.RES_FILE_NAME
!byte 0
!byte 0,0,0,0,0,0,0,0,0,0
!byte 0,0,0,0,0,0,0,0,0,0
!byte 0,0,0,0,0,0,0,0,0,0
!byte 0,0,0,0,0,0,0,0,0,0

.LOAD_FILE_NAME
!byte 11
!tx "LOAD FROM: "
.PREFIX_SLOT
!byte 2
!tx "0:"
.POSTFIX_SEQ_READ
!byte 4
!tx ",S,R"

; --------------------------------------------------
; loadCommand
; --------------------------------------------------
loadCommand
    lda #0
    sta .RES_FILE_NAME
    lda #16
    sta .FILE_NAME

    +printStr .LOAD_FILE_NAME
    +inputStr .FILE_NAME, .ALPHA
    lda .FILE_NAME
    beq .emptyFileName

    +strCpy .PREFIX_SLOT, .RES_FILE_NAME
    +strCat .FILE_NAME, .RES_FILE_NAME
    +strCat .POSTFIX_SEQ_READ, .RES_FILE_NAME
    +strCpy .RES_FILE_NAME, PIC_FILE_NAME_LEN

    jsr loadPic
    lda DISK_ERROR
    beq .waitForKey
    jsr printCRLF
    +printStr ERR_BYTE_COUNTER
    jsr printCRLF
    rts
.waitForKey
    jsr turnBitmapOn
    jsr waitForKey
    jsr bitMapOff
    jsr printCRLF
    rts
.emptyFileName
    jsr printCRLF
    +printStr .ERR_FILE_NAME_EMPTY
    jsr printCRLF
    rts

    
.INPUT_FILE_NAME
!byte 9
!tx "SAVE IN: "
.PREFIX_OVERWRITE
!byte 3
!tx "@0:"
.POSTFIX_SEQ
!byte 4
!tx ",S,W"

; --------------------------------------------------
; saveCommand
; --------------------------------------------------
saveCommand
    lda #0
    sta .RES_FILE_NAME
    lda #16
    sta .FILE_NAME

    +printStr .INPUT_FILE_NAME
    +inputStr .FILE_NAME, .ALPHA
    lda .FILE_NAME
    beq .emptyFileNameSave
    
    +strCpy .PREFIX_OVERWRITE, .RES_FILE_NAME
    +strCat .FILE_NAME, .RES_FILE_NAME
    +strCat .POSTFIX_SEQ, .RES_FILE_NAME
    +strCpy .RES_FILE_NAME, PIC_FILE_NAME_LEN
    jsr savePic
    jsr printCRLF
    rts
.emptyFileNameSave
    jsr printCRLF
    +printStr .ERR_FILE_NAME_EMPTY
    jsr printCRLF
    rts


; --------------------------------------------------
; showCommand
; --------------------------------------------------
showCommand
    jsr turnBitmapOn
    jsr waitForKey
    jsr bitMapOff
    jsr printCRLF
    rts


.INP_X_COORD
!byte 3
!tx "   "
.INP_Y_COORD
!byte 3
!tx "   "
.TXT_X_COORD
!byte 9
!tx "X COORD: "
.TXT_Y_COORD
!byte 9
!tx "Y COORD: "
.ERR_OUT_OF_RANGE
!byte 18
!tx "VALUE OUT OF RANGE"
.ERR_NO_CONVERT
!byte 15
!tx "CAN NOT CONVERT"


.CONV_RESULT_X
!byte 0,0
.CONV_RESULT_Y
!byte 0,0
.TWO_HUNDRED
!byte 200,0
; --------------------------------------------------
; deriveFromPixelCommand
; --------------------------------------------------
deriveFromPixelCommand
    lda #3
    sta .INP_X_COORD
    sta .INP_Y_COORD

    +printStr .TXT_X_COORD
    +inputStr .INP_X_COORD, .DIGITS
    +atoi .INP_X_COORD, .CONV_RESULT_X
    bne .convErr
    +cmp16Bit .CONV_RESULT_X, MAX_X
    bcs .tooLargeErr
    jsr printCRLF

    +printStr .TXT_Y_COORD
    +inputStr .INP_Y_COORD, .DIGITS
    +atoi .INP_Y_COORD, .CONV_RESULT_Y
    bne .convErr
    +cmp16Bit .CONV_RESULT_Y, .TWO_HUNDRED
    bcs .tooLargeErr
    jsr printCRLF

    +move16Bit .CONV_RESULT_X, COUNT_X
    lda .CONV_RESULT_Y
    sta COUNT_Y
    jsr deriveParametersFromPixel
    jmp showValuesCommand
.convErr
    jsr printCRLF
    +printStr .ERR_NO_CONVERT
    jsr printCRLF
    rts
.tooLargeErr
    jsr printCRLF
    +printStr .ERR_OUT_OF_RANGE
    jsr printCRLF
    rts


; --------------------------------------------------
; incrementZoomCommand
; --------------------------------------------------
incrementZoomCommand
    jsr increaseZoomLevel
    jmp showValuesCommand


; --------------------------------------------------
; multicolorCommand
; --------------------------------------------------
multicolorCommand
    jsr setMuticolor
    rts


; --------------------------------------------------
; hiresCommand
; --------------------------------------------------
hiresCommand
    jsr setHires
    rts


; --------------------------------------------------
; doubleXstepCommand
; --------------------------------------------------
doubleXstepCommand
    jsr doubleStepX
    jmp showValuesCommand


; --------------------------------------------------
; halveXstepCommand
; --------------------------------------------------
halveXstepCommand
    jsr halveStepX
    jmp showValuesCommand  
  

; --------------------------------------------------
; resetCommand
; --------------------------------------------------
resetCommand
    jsr resetParameters
    jmp showValuesCommand


.TXT_MAX_ITER
!byte 10
!tx "MAX ITER: "
.INP_ITER
!byte 3
!tx "   "
.CONV_RESULT_ITER
!byte 0,0
.MAX_ITER_CMP
!byte 255,0
; --------------------------------------------------
; setMaxIterCommand
; --------------------------------------------------
setMaxIterCommand
    lda #3
    sta .INP_ITER

    +printStr .TXT_MAX_ITER
    +inputStr .INP_ITER, .DIGITS
    +atoi .INP_ITER, .CONV_RESULT_ITER
    bne .convErrIter
    +cmp16Bit .CONV_RESULT_ITER, .MAX_ITER_CMP
    bcs .tooLargeErrIter
    jsr printCRLF
    lda .CONV_RESULT_ITER
    sta MAX_ITER
    jmp showValuesCommand
.convErrIter
    jsr printCRLF
    +printStr .ERR_NO_CONVERT
    jsr printCRLF
    rts
.tooLargeErrIter
    jsr printCRLF
    +printStr .ERR_OUT_OF_RANGE
    jsr printCRLF
    rts


.TXT_DEVICE_NUMBER
!byte 15
!tx "DEVICE NUMBER: "
.DEV_NUM_BUFFER
!byte 2
!byte 0,0
.NEW_DEV_NUM
!byte 0,0

; --------------------------------------------------
; setDeviceNumberCommand
; --------------------------------------------------
setDeviceNumberCommand
    lda #2
    sta .DEV_NUM_BUFFER
    +printStr .TXT_DEVICE_NUMBER
    +inputStr .DEV_NUM_BUFFER, .DIGITS
    +atoi .DEV_NUM_BUFFER, .NEW_DEV_NUM
    bne .convErrDevNum
    lda .NEW_DEV_NUM
    cmp #12
    bcs .tooLargeErrDevNum
    sta DEVICE_NUM
    jsr printCRLF
    jmp showValuesCommand

.convErrDevNum
    jsr printCRLF
    +printStr .ERR_NO_CONVERT
    jsr printCRLF
    rts
.tooLargeErrDevNum
    jsr printCRLF
    +printStr .ERR_OUT_OF_RANGE
    jsr printCRLF
    rts


.FIRST_COLOR_TXT
!byte 10
!tx "FIRST   : "
.SECOND_COLOR_TXT
!byte 10
!tx "SECOND  : "
.THIRD_COLOR_TXT
!byte 10
!tx "THIRD   : "
.BKGRND_COLOR_TXT
!byte 10
!tx "BACKGRND: "

.PIC_COL1
!byte 0
.PIC_COL2
!byte 0
.PIC_COL3
!byte 0
.BACKGROUND_COLOR
!byte 0
; --------------------------------------------------
; setColorsCommand
; --------------------------------------------------
setColorsCommand
    +printStr .FIRST_COLOR_TXT
    jsr getColor
    bcs .doneError
    sta .PIC_COL1

    +printStr .SECOND_COLOR_TXT
    jsr getColor
    bcs .doneError
    sta .PIC_COL2

    +printStr .THIRD_COLOR_TXT
    jsr getColor
    bcs .doneError
    sta .PIC_COL3

    +printStr .BKGRND_COLOR_TXT
    jsr getColor
    bcs .doneError
    sta .BACKGROUND_COLOR

    lda .PIC_COL2
    asl
    asl
    asl
    asl
    ora .PIC_COL1
    sta PIC_COLOURS
    lda .PIC_COL3
    sta THIRD_COLOUR
    lda .BACKGROUND_COLOR
    sta BACKGROUND_COLOR
    rts    
.doneError
    rts


; --------------------------------------------------
; showColorsCommand
; --------------------------------------------------
showColorsCommand
    +printStr .FIRST_COLOR_TXT
    lda PIC_COLOURS
    and #$0F
    jsr printByteCRLF

    +printStr .SECOND_COLOR_TXT
    lda PIC_COLOURS
    lsr
    lsr
    lsr
    lsr
    jsr printByteCRLF

    +printStr .THIRD_COLOR_TXT
    lda THIRD_COLOUR
    and #$0F
    jsr printByteCRLF

    +printStr .BKGRND_COLOR_TXT
    lda BACKGROUND_COLOR
    and #$0F
    jsr printByteCRLF

    rts

.COLOR_NUM_BUFFER
!byte 2
!byte 0,0
.NEW_COL_NUM
!byte 0,0
; --------------------------------------------------
; This routine reads a color number from the keyboard.
; 
; The color number is returned in the accu. The value in the accu is valid
; if the carry bit is clear.
; --------------------------------------------------
getColor
    lda #2
    sta .COLOR_NUM_BUFFER
    +inputStr .COLOR_NUM_BUFFER, .DIGITS
    +atoi .COLOR_NUM_BUFFER, .NEW_COL_NUM
    bne .convErrColNum
    lda .NEW_COL_NUM
    cmp #16
    bcs .tooLargeErrColNum
    jsr printCRLF
    lda .NEW_COL_NUM
    clc                               ; unnecessary, but clearer => indicate success
    rts

.convErrColNum
    jsr printCRLF
    +printStr .ERR_NO_CONVERT
    jsr printCRLF
    sec                               ; indicate error
    rts
.tooLargeErrColNum
    jsr printCRLF
    +printStr .ERR_OUT_OF_RANGE
    jsr printCRLF
    sec                               ; indicate error
    rts


CROSSHAIR_ICON
!byte %00000000, %00111100, %00000000
!byte %00000000, %00111100, %00000000
!byte %00000000, %00111100, %00000000
!byte %00000000, %00111100, %00000000
!byte %00000000, %00111100, %00000000
!byte %00000000, %00111100, %00000000
!byte %00000000, %00111100, %00000000
!byte %00000000, %00111100, %00000000
!byte %00000000, %00111100, %00000000
!byte %11111111, %00000000, %11111111
!byte %11111111, %00000000, %11111111
!byte %11111111, %00000000, %11111111
!byte %00000000, %00111100, %00000000
!byte %00000000, %00111100, %00000000
!byte %00000000, %00111100, %00000000
!byte %00000000, %00111100, %00000000
!byte %00000000, %00111100, %00000000
!byte %00000000, %00111100, %00000000
!byte %00000000, %00111100, %00000000
!byte %00000000, %00111100, %00000000
!byte %00000000, %00111100, %00000000


.SPRITE_X_POS
!byte 0,0
.SPRITE_Y_POS
!byte 100
; --------------------------------------------------
; crosshairSymbolOn shows a crosshair sprite on screen
;
; INPUT:  None
; OUTPUT: None
; --------------------------------------------------
crosshairSymbolOn
    ldx #0
.copySprite
    lda CROSSHAIR_ICON, x
    sta SPRITE_0_ADDRESS, x
    inx
    cpx #63
    bne .copySprite

    lda #0
    sta .SPRITE_X_POS+1
    lda #100
    sta .SPRITE_X_POS
    sta .SPRITE_Y_POS

    ; set sprite block for sprite 0
    lda #SPRITE_0_BLOCK
    sta SPRITE_0_VECTOR

    ; set sprite X position for sprite 0
    lda .SPRITE_X_POS
    sta VIC_BASE
    lda VIC_BASE + 16
    ora .SPRITE_X_POS+1
    sta VIC_BASE + 16
    ; set sprite 0 Y position
    lda .SPRITE_Y_POS
    sta VIC_BASE + 1

    ; set color of sprite 0
    lda #SPRITE_COLOR
    sta VIC_BASE + 39

    ; turn sprite 0 on
    lda VIC_BASE +  21
    ora #1
    sta VIC_BASE + 21

    rts


; --------------------------------------------------
; crosshairSymbolOff turns off a crosshair sprite on screen
;
; INPUT:  None
; OUTPUT: None
; --------------------------------------------------
crosshairSymbolOff
    ; turn sprite 0 off
    lda VIC_BASE +  21
    and #$FE
    sta VIC_BASE + 21

    rts


.MIN_SPRITE_Y
!byte 40
.MAX_SPRITE_Y
!byte 239
.MIN_SPRITE_X
!byte 12,0
.MAX_SPRITE_X
!byte $4c, 1
; --------------------------------------------------
; Use a joystick in port 2 to control a crosshair sprite on the screen
; in order to select a new start value used for a following calculation.
; This routine returns when the fire button is pressed.
;  
; INPUT:  None
; OUTPUT: None
; --------------------------------------------------
waitForJoystick
.loopJoy
    lda JOYSTICK2
    cmp #$7e          ; up
    beq .upFound
    cmp #$7d          ; down
    beq .downFound
    cmp #$7b          ; left
    beq .leftFound
    cmp #$77          ; right   
    beq .rightFound
    cmp #$6f          ; fire
    beq .quitFound
    bne .loopJoy
.upFound
    lda .SPRITE_Y_POS
    cmp .MIN_SPRITE_Y
    beq .loopJoy
    dec .SPRITE_Y_POS
    jmp .setSpritePos
.downFound
    lda .SPRITE_Y_POS
    cmp .MAX_SPRITE_Y
    beq .loopJoy
    inc .SPRITE_Y_POS
    jmp .setSpritePos
.leftFound
    +cmp16Bit .MIN_SPRITE_X, .SPRITE_X_POS
    beq .loopJoy
    +dec16Bit .SPRITE_X_POS
    jmp .setSpritePos
.rightFound
    +cmp16Bit .MAX_SPRITE_X, .SPRITE_X_POS
    beq .loopJoy
    +inc16Bit .SPRITE_X_POS
    jmp .setSpritePos
.quitFound
    rts
.setSpritePos
    ; set sprite X position for sprite 0
    lda .SPRITE_X_POS
    sta VIC_BASE
    lda VIC_BASE + 16
    and #%11111110
    ora .SPRITE_X_POS+1
    sta VIC_BASE + 16

    ;set sprite Y position for sprite 0
    lda .SPRITE_Y_POS
    sta VIC_BASE + 1

    ldy #0
    ldx #0
.pause
    inx
    bne .pause
    iny
    cpy #NUM_DELAY_LOOPS
    bne .pause

    jmp .loopJoy


; --------------------------------------------------
; selectPixelCommand
; --------------------------------------------------
selectPixelCommand
    jsr turnBitmapOn
    jsr crosshairSymbolOn
    jsr waitForJoystick
    jsr crosshairSymbolOff
    jsr bitMapOff

    lda .SPRITE_Y_POS
    sec
    sbc .MIN_SPRITE_Y
    sta COUNT_Y

    +sub16Bit .MIN_SPRITE_X, .SPRITE_X_POS
    +cmp16BitImmediate 319, .SPRITE_X_POS        ; carry is set if 319 >= *.SPRITE_X_POS
    bcs .noCorrection                            
    +load16BitImmediate 319, .SPRITE_X_POS       ; value is >= 320 set value to 319 instead
.noCorrection
    +move16Bit .SPRITE_X_POS, COUNT_X

    jsr transformXCoord                          ; adapt x-coordinate accordingly in case a multicolor bitmap is used
    
    +printStr .TXT_Y_COORD
    lda COUNT_Y
    jsr printByteCRLF

    +printStr .TXT_X_COORD
    lda COUNT_X+1
    jsr printByte
    lda COUNT_X
    jsr printByteCRLF
    jsr printCRLF

    jsr deriveParametersFromPixel
    jmp showValuesCommand



.COMMAND_NOT_IMPLEMENTED
!byte 19
!tx "NOT YET IMPLEMENTED"
; --------------------------------------------------
; helpCommand
; --------------------------------------------------
helpCommand
    ldx #0
.commandLoop
    lda .CMD_TABLE,x
    sta TEMP_PTR
    inx
    lda .CMD_TABLE,x
    sta TEMP_PTR+1
    +cmp16Bit .NO_COMMAND, TEMP_PTR
    beq .allPrinted
    txa
    pha
    jsr printString
    jsr printCRLF
    pla
    tax
    inx
    inx
    inx
    jmp .commandLoop 

.allPrinted
    rts

